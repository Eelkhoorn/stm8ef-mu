loading work/MINDEV/ADF/ADF
-- Register calculaion for ADF4351, range 33 - 4294.9 MHz 
-- Connect PD2 to LE, C5 (CLK) to CLK and C6 to DAT.

ld target/STM8/MINDEV.mu4
ld aliases

flash

ld work/lib/SH1106-SPI
ld work/lib/D+!
ld work/lib/D*
ld work/lib/UD<
ld work/lib/D:

__meta

eeprom
here constant EPR
hex
1E3 , C448 ,
800 , E1A1 ,
  2 ,  E42 ,
  0 ,  4B3 ,
 C1 , 9404 ,
 58 ,    5 ,
decimal

flash
VARIABLE RR 22 allotr		-- array for 6 32 bit regs
VARIABLE Fro 2 allotr		-- output freq
VARIABLE Fvco 2 allotr		-- VCO freq
VARIABLE Fpfd 2 allotr
VARIABLE Res 2 allotr
VARIABLE Int
VARIABLE Frac
VARIABLE Mod				-- modulus
VARIABLE Fdv
CVAR Pow

-- Power of two
: p2 ( n -- n )
	1 SWAP DUP if
		0do1 [ $6801 , ( SLL (1,X) ] loop1
	else
		DROP
	then
;
-- $9D9D , $9D9D , $9D c, 

-- calculate Fdv, Fvco, Int, Frac and Mod
: Calc ( -- )
	6 0 $400			-- 67108864 (=$1.0000.0000/64)
	begin
		2DUP 1- Fro 2@ UD< while
		ROT 1- ROT ROT 2 D*
	repeat
	2DROP DUP [ Fdv ]!
	p2 Fro 2@ ROT D* 2DUP Fvco 2!
	( Fvco ) Fpfd 2@ D/ DROP [ Int ]!
	Res 2@ D/ DROP [ Frac ]! 2DROP
	Fpfd 2@ Res 2@ D/ DROP [ Mod ]! 2DROP
;

-- set/reset bits in double
: sba ( a u i n -- )
	OVER + SWAP do1 2DUP 1 AND i1 ROT SWAP BF! 2/ loop1 2DROP
;

-- update reg array
: ua
	RR [ Frac ]@ #19 #12 sba
	RR [ Int ]@ 2DUP 1 AND #31 1 sba
	2/ 0 #15 sba
	RR 4 + [ Mod ]@ #19 #12 sba
	RR #16 + DUP [ Fdv ]@ 4 3 sba
	[ Pow ]C@ 3 + 19 3 sba
;

: pls  ( -- )	-- give pulse on LE
	[ 1 PD_ODR 2 ]B!
	[ 0 PD_ODR 2 ]B!
;

-- send 4 bytes starting at address a
: s4 ( a -- )
	-o		-- disable oled, (on same spi bus)
	4 0do1 DUP i1 + C@ >S loop1 pls DROP
	+o
;

-- send all 6 registersdata
: send ( a-- )
	5 for1 DUP i1 4* + s4 next1 DROP
;

-- update regs on board
: ud
	ua RR send
;

: init0 ( -- )
	EPR RR #24 CMOVE
	[ 1 PD_DDR 2 ]B!
	[ 1 PD_CR1 2 ]B!
	0 SPIon
	RR send
;

ld work/MINDEV/ADF/ADF+

comment ===

RFOUT = [INT + (FRAC/MOD)] Ã— (fPFD/RF Divider)
resolution = fPFD/(MOD x RF Divider)
fPFD = RFin/

Int		R0	15-30
Frac	R0	3-14
Mod		R1	3-14
Fdv		R4	20-22
OE		R4	5		output enable 0|1
OP		R4	3-4		output power  0..3

===
